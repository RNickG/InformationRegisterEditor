
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтаФорма.Заголовок = "Хранилище значения: "+Параметры.ИмяРеквизита;
	ВходныеПараметры = Новый Структура;
	ВходныеПараметры.Вставить("КлючЗаписи", Параметры.КлючЗаписи);
	ВходныеПараметры.Вставить("ИмяРегистраСведений", Параметры.ИмяРегистраСведений);
	ВходныеПараметры.Вставить("ИмяРеквизита", Параметры.ИмяРеквизита);
	
	ТипЗначенияВХранилищеСтрока = Строка(ТипЗнч(ПрочитатьЗначение()));
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДвоичныеДанныеНаСервере(АдресВХранилище)
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ЗаписатьЗначение(ДвоичныеДанные);
	Сообщить("Загрузка двоичных данных завершена.", СтатусСообщения.Внимание);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДвоичныеДанные(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = НСтр("Все файлы|*.*");
	Диалог.Заголовок = НСтр("Выберите произвольный файл...");
	Диалог.МножественныйВыбор = Ложь;
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаЗагрузитьДвоичныеДанныеЗавершение", ЭтотОбъект);
	Диалог.Показать(ОповещениеЗавершения);	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗагрузитьДвоичныеДанныеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ДвоичныеДанные = Новый ДвоичныеДанные(ВыбранныеФайлы[0]);
	АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	ЗагрузитьДвоичныеДанныеНаСервере(АдресВХранилище);
КонецПроцедуры

&НаСервере
Функция  ВыгрузитьДвоичныеДанныеНаСервере()
	Значение = ПрочитатьЗначение();
	Если ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
		Возврат ПоместитьВоВременноеХранилище(Значение);
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ВыгрузитьДвоичныеДанные(Команда)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("Все файлы|*.*");
	Диалог.Заголовок = НСтр("Выберите произвольный файл...");
	ОповещениеЗавершения = Новый ОписаниеОповещения("ВыборФайлаВыгрузитьДвоичныеДанныеЗавершение", ЭтотОбъект);
	Диалог.Показать(ОповещениеЗавершения);	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаВыгрузитьДвоичныеДанныеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	АдресВХранилище = ВыгрузитьДвоичныеДанныеНаСервере();
	Если не АдресВХранилище = Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);
		ДвоичныеДанные.Записать(ВыбранныеФайлы[0]);
		Сообщить("Выгрузка двоичных данных завершена.",СтатусСообщения.Внимание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьВXMLНаСервере()
	Значение = ПрочитатьЗначение();
	
	Сериализатор = СериализаторXDTO;
	
	Путь = КаталогВременныхФайлов();
	ИмяВременногоФайла = Путь + Строка(Новый УникальныйИдентификатор) + ".xml";
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла, "UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("_1CV8DtUD", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("V8Exch", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("core", "http://v8.1c.ru/data");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8", "http://v8.1c.ru/8.1/data/enterprise/current-config");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xs", "http://www.w3.org/2001/XMLSchema");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("V8Exch:Data");
	
	Попытка 
		Сериализатор.ЗаписатьXML(ЗаписьXML, Значение);
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); //V8Exc:Data
		ЗаписьXML.ЗаписатьКонецЭлемента(); //V8Exc:_1CV8DtUD
		ЗаписьXML.Закрыть();
		
	Исключение
		Сообщить(ОписаниеОшибки(), СтатусСообщения.Важное);
		Возврат Неопределено;
	КонецПопытки;
	
	ДД = Новый ДвоичныеДанные(ИмяВременногоФайла);
	АдресХранилища = ПоместитьВоВременноеХранилище(ДД);
	
	УдалитьФайлы(Путь, ИмяВременногоФайла);
	
	Возврат АдресХранилища;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьВXML(Команда)
	ПоказатьДиалогXML(Новый ОписаниеОповещения("ВыборФайлаВыгрузитьXMLЗавершение", ЭтаФорма), РежимДиалогаВыбораФайла.Сохранение);
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаВыгрузитьXMLЗавершение(ВыбранныеФайлы, ДополнительныеПараметр) Экспорт 
	Если ВыбранныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	РезультатВыгрузки = ВыгрузитьВXMLНаСервере();
	Если Не РезультатВыгрузки = Неопределено Тогда
		ДД = ПолучитьИзВременногоХранилища(РезультатВыгрузки);
		ДД.Записать(ВыбранныеФайлы[0]);
		Сообщить("Выгрузка в XML завершена.",СтатусСообщения.Внимание);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзXMLНаСервере(АдресВХранилище)
	
	Путь = КаталогВременныхФайлов();
	ИмяВременногоФайла = Путь + Строка(Новый УникальныйИдентификатор)+".xml";
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяВременногоФайла);
	// проверка формата файла обмена
	Если Не ЧтениеXML.Прочитать()
		Или ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента
		Или ЧтениеXML.ЛокальноеИмя <> "_1CV8DtUD"
		Или ЧтениеXML.URIПространстваИмен <> "http://www.1c.ru/V8/1CV8DtUD/" Тогда
		
		Сообщить("Неверный формат файла выгрузки", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Не ЧтениеXML.Прочитать()
		Или ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента
		Или ЧтениеXML.ЛокальноеИмя <> "Data" Тогда
		
		Сообщить("Неверный формат файла выгрузки", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Если Не ЧтениеXML.Прочитать() Тогда 
		Сообщить("Неверный формат файла выгрузки", СтатусСообщения.Важное);
		Возврат;
	КонецЕсли;
	
	Сериализатор = СериализаторXDTO;
	
	Пока Сериализатор.ВозможностьЧтенияXML(ЧтениеXML) Цикл
		
		Попытка
			Значение = Сериализатор.ПрочитатьXML(ЧтениеXML);
			ЧтениеXML.Закрыть();
			ЗаписатьЗначение(Значение);
			ПроизвольноеЗначение = Неопределено;
			УдалитьФайлы(Путь, ИмяВременногоФайла);
			Сообщить("Загрузка из XML завершена.",СтатусСообщения.Внимание);			
		Исключение
			Сообщить(ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		
	КонецЦикла;	
			
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзXML(Команда)
	ПоказатьДиалогXML(Новый ОписаниеОповещения("ВыборФайлаЗагрузитьXMLЗавершение", ЭтаФорма), РежимДиалогаВыбораФайла.Открытие);
КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаЗагрузитьXMLЗавершение(ВыбранныеФайлы, ДополнительныеПараметр) Экспорт 
	Если ВыбранныеФайлы = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ДвоичныеДанные = Новый ДвоичныеДанные(ВыбранныеФайлы[0]);
	АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	ЗагрузитьИзXMLНаСервере(АдресВХранилище);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогXML(ОписаниеОповещения, РежимДиалога)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Фильтр = "Файл XML (*.xml)|*.xml";
	ДиалогВыбораФайла.Показать(ОписаниеОповещения);
КонецПроцедуры

&НаСервере
Процедура ПроизвольноеЗначениеПриИзмененииНаСервере()
	ЗаписатьЗначение(ПроизвольноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ПроизвольноеЗначениеПриИзменении(Элемент)
	ПроизвольноеЗначениеПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Функция ПрочитатьЗапись()
	МЗ = РегистрыСведений[ВходныеПараметры.ИмяРегистраСведений].СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МЗ,ВходныеПараметры.КлючЗаписи);
	МЗ.Прочитать();
	Возврат МЗ;
КонецФункции

&НаСервере
Функция ПрочитатьЗначение()
	МЗ = ПрочитатьЗапись();
	Возврат МЗ[ВходныеПараметры.ИмяРеквизита].Получить();
КонецФункции

&НаСервере
Функция ЗаписатьЗначение(Значение)
	МЗ = ПрочитатьЗапись();
	МЗ[ВходныеПараметры.ИмяРеквизита] = Новый ХранилищеЗначения(Значение);
	МЗ.Записать();
	ТипЗначенияВХранилищеСтрока = Строка(ТипЗнч(Значение));
КонецФункции

&НаКлиенте
Процедура КомандаОткрытьЗначение(Команда)
	ПоказатьЗначение(, ПрочитатьЗначение());
КонецПроцедуры


&НаСервере
Функция ПросмотрЗначенияВXMLНаСервере()
	Значение = ПрочитатьЗначение();
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку(XMLСтрока(Значение));
	Возврат ПоместитьВоВременноеХранилище(Текст);
КонецФункции


&НаКлиенте
Процедура ПросмотрЗначенияВXML(Команда)
	Текст = ПолучитьИзВременногоХранилища(ПросмотрЗначенияВXMLНаСервере());
	Текст.Показать("");
КонецПроцедуры

